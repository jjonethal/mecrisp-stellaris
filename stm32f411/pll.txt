\ file:    PLL.TXT
\ summary: clock programming for stm32f411 nucleo board
\ author:  jean jonethal
\ license: public domain

\ 2016-05-28jjo update header
\ 2016-05-27jjo add usart cleanup
\ 2016-05-26jjo reformat code
\ 2016-05-20jjo collects functions for reprogramming PLL


\ datasheet stm32f411 "E:\stm\DM00115249 STM32F411xC STM32F411xE.pdf"
\ "C:\Users\jeanjo\Downloads\stm\DM00115249 stm32f411re data sheet.pdf"
\ "C:\Users\jeanjo\Downloads\stm\DM00119316 stm32f411re reference manual.pdf"
\ progman "E:\stm\DM00046982 PM0214 STM32F3 and STM32F4 Series Cortex-M4 programming manual.pdf"
\ ref man "E:\stm\DM00119316  RM0383 STM32F411xC_E advanced ARM-based 32-bit MCUs .pdf"

\ Requires bits! bits@ from util.txt

\ include util.txt

#8000000 constant HSE_CLK                \ 8 MHz system HSE clock on Nucleo


\ ***** USART access ********************
$40011000 constant USART1                \ base address for usart1
$40004400 constant USART2                \ base address for usart2
$40011400 constant USART6                \ base address for usart6
USART2    constant USART_TERM            \ terminal USART
#8 constant USART_BRR                    \ Baud rate register offset

0            constant USART_SR           \ usart status register offset
#1 #7 lshift constant USART_SR_TXE       \ USART Transmission complete
#1 #6 lshift constant USART_SR_TC        \ USART Transmission complete
USART2 USART_BRR or constant USART2_BRR
$0C          constant USART_CR1          \ usart cr1 reg offset
#1 #13 lshift constant USART_CR1_UE      \ usart enable bit
: usart-set-baud  ( usart BAUD clk -- )  \ usart - base address
   over 2/ + swap / swap                 \ baud rate
   USART_BRR or ! ;                      \ usart base frequency for 16 times oversampling mode
: usart-tx-complete?  ( u -- f )         \ check is usart transfer is complete
   @ USART_SR_TC USART_SR_TXE or
   and 0<> ;
: wait-usart-tx-complete ( u -- )        \ wait until usart tx is complete
   begin dup usart-tx-complete? until
   drop ;
: usart-active? ( u -- f )
   USART_CR1 or USART_CR1_UE swap bit@ ;
: usart-enable ( u -- )
   USART_CR1 or USART_CR1_UE swap bis! ;
: usart-disable ( u -- )
   USART_CR1 or USART_CR1_UE swap bic! ;

\ ***** stm32f411re clock tree **********
\ RTCCLK <- (RTC enable & RTCSEL)
\ RTCSEL <- (OFF[0]|LSE[1]|LSI[2]|HSE_RTC[3])
\ HSE_RTC <- HSE/RTCPRE[2-31;0,1-clock off]

\ ***** flash control register **********
$40023C00 constant Flash_ACR             \ Flash Access Control Register
: dcache-ena? ( -- f )                   \ is dcache enabled
   1 #10 lshift FLASH_ACR bit@ ;
: dcache-ena  ( -- )                     \ enable dcache
   1 #10 lshift FLASH_ACR bis! ;
: dcache-dis  ( -- )                     \ disable dcache
   1 #10 lshift FLASH_ACR bic! ;
: dcache-reset ( -- )                    \ reset dcache
   dcache-ena? dcache-dis
   1 #12 lshift FLASH_ACR bis!
   1 #12 lshift FLASH_ACR bic!
   if dcache-ena then ;
: icache-ena? ( -- f )                   \ instruction cache enabled
   1 #9 lshift FLASH_ACR bit@ ;
: icache-ena  ( -- )                     \ enable instruction cache
   1 #9 lshift FLASH_ACR bis! ;
: icache-dis  ( -- )                     \ disable instruction cache
   1 #9 lshift FLASH_ACR bic! ;
: icache-reset ( -- )                    \ reset instruction cache
   icache-ena? dcache-dis
   1 #11 lshift FLASH_ACR bis!
   1 #11 lshift FLASH_ACR bic!
   if icache-ena then ;
: prefetch-ena ( -- )                    \ enable prefetch
   1 #8 lshift FLASH_ACR bis! ;
: prefetch-dis ( -- )                    \ disable prefetch
   1 #8 lshift FLASH_ACR bic! ;
: flash-latency! ( l -- )                \ set flash latency
   $f FLASH_ACR bits! ;
: cache-init  ( -- )
   icache-reset icache-ena
   dcache-reset dcache-ena ;

$40004408 constant USART2_BRR

\ ***** System clock settings ***********
\ f (VCO clock) = f (PLL clock input) * (PLLN/PLLM)
\ f (PLL general clock output) = F (VCO clock) / PLLP
\ f (USB, RNG und andere) = f (VCO clock) / PLLQ

\ ***** RCC register definitions ********
$40023800      constant RCC_Base

RCC_Base $00 +  constant RCC_CR
    1           constant HSION
    1  1 lshift constant HSIRDY
  $1F  3 lshift constant HSITRIM
  $FF  8 lshift constant HSICAL
    1 16 lshift constant HSEON
    1 17 lshift constant HSERDY
    1 18 lshift constant HSEBYP
    1 19 lshift constant CSSON
    1 24 lshift constant PLLON
    1 25 lshift constant PLLRDY
    1 26 lshift constant PLLI2SON
    1 27 lshift constant PLLI2SRDY


RCC_Base $04 +    constant RCC_PLLCFGR
   $03F           constant PLLM    \ PLL input predivider after PLL source mux 1..2MHz
   $1FF  6 lshift constant PLLN    \ PLL multiplier after PLLM 100...432MHz
      3 16 lshift constant PLLP    \ divider after PLLN - 0: /2 1: /4 2: /6 3: /8 <100MHz
      1 16 lshift constant PLLP0
      1 17 lshift constant PLLP1
      1 22 lshift constant PLLSRC  \ PLLCLK source for PLL and PLLI2S 0-HSI 1-HSE
     $F 24 lshift constant PLLQ    \ PLL divider for USB otg(48MHz), SDIO(<=48MHz), RNG(<=48MHZ) after PLLN
      1 24 lshift constant PLLQ0   \ PLL divider for USB otg(48MHz), SDIO(<=48MHz), RNG(<=48MHZ) after PLLN
      1 25 lshift constant PLLQ1   \ valid values 2..15
      1 26 lshift constant PLLQ2
      1 27 lshift constant PLLQ3


RCC_Base $08 + constant RCC_CFGR
      3 30 lshift constant MC02    \ Microcontroller clock output 2 - 00: SYSCLK 01: PLLI2S 10: HSE 11: PLL
      7 27 lshift constant MCO2PRE \ MCO2 prescaler - 0xx: /1 100: /2 101: /3 110: /4 111: /5
      7 24 lshift constant MCO1PRE \ MCO1 prescaler - 0xx: /1 100: /2 101: /3 110: /4 111: /5
      1 23 lshift constant I2SSRC  \ I2S clock selection - 0: PLLI2S 1: External clock on the I2S_CKIN pin
      3 21 lshift constant MCO1    \ Microcontroller clock output 1 - 00: HSI 01: LSE 10: HSE 11: PLL
     31 16 lshift constant RTCPRE  \ HSE division factor for RTC clock 0,1: no clock, 2..31: /2../31
      7 13 lshift constant PPRE2   \ APB high-speed prescaler (APB2) 0xx: /1, 100: /2, 101: /4, 110: /8, 111: /16 <= 84Mhz
      7 10 lshift constant PPRE1   \ APB Low speed prescaler (APB1) 0xx: /1, 100: /2, 101: /4, 110: /8, 111: /16 <= 42Mhz
      3  8 lshift constant RCC_CFGR_RES \ reserved
     15  4 lshift constant HPRE    \ AHB prescaler 0-7:/1 8:/2 9:/4 10:/8 11:/16 12:/64 13:/128 14:/256 15:/512 (>=25 for Ethernet)
      3  2 lshift constant SWS     \ System clock switch status 0:HSI 1:HSE 2:PLL 3:not applicable
      3           constant SW      \ System clock switch	  0:HSI 1:HSE 2:PLL 3:not allowed


: HSE_BYPASS  ( -- )                     \ external clock source
  HSEBYP HSEON RCC_CR BIS! ;
: HSE_XTAL ( -- )                        \ xtal
  RCC_CR @ HSEBYP not and HSEON or RCC_CR ! ;
: pll-off  ( -- ) PLLON RCC_CR bic! ;    \ turn off PLL
: pll-on   ( -- ) PLLON RCC_CR bis! ;    \ turn on PLL
: pll-src-hse  ( -- )                    \ PLL source is hse
   PLLSRC RCC_PLLCFGR bis! ;
: pllm!  ( m -- )                        \ set PLL predivider
   PLLM RCC_PLLCFGR bits! ;
: plln!  ( n -- )                        \ set PLL multiplier
   PLLN RCC_PLLCFGR bits! ;
: pllp!  ( p -- )                        \ set PLL output divider
   PLLP RCC_PLLCFGR bits! ;
: hpre!  ( n -- ) HPRE RCC_CFGR bits! ;  \ set AHB prescaler
: ppre2!  ( n -- )                       \ set highspeed APB prescaler (APB2)
   PPRE2 RCC_CFGR bits! ;
: ppre1!  ( n -- )                       \ set low speed APB prescaler (APB1)
   PPRE1 RCC_CFGR bits! ;
: hse-on! ( -- ) HSEON RCC_CR bis! ;     \ turn on hse
: hse-on? ( -- f ) HSERDY RCC_CR bit@ ;  \ is hse on ?
: wait-hse ( n -- f )                    \ wait until hse is ready return true
   0 do hse-on? if unloop true exit then \ after n polls return false
   loop false ;                          \ this will poll maximum n times
: sys-clk-hse  ( -- )                    \ set system clock to hse
   #1 SW RCC_CFGR bits! ;
: sys-clk-hsi  ( -- )                    \ set system clock to hsi
   #0 SW RCC_CFGR bits! ;
: sys-clk-pll  ( -- )                    \ set system clock to PLL mode
   #2 SW RCC_CFGR bits! ;
: sys-clk  ( mhz -- )                    \ set system clock to MHZ
   hse-on? if sys-clk-hse
   else sys-clk-hsi then ;
: clk-mhz-hse  ( mhz latency -- )        \ set system clock and flash latency
   USART_TERM wait-usart-tx-complete     \ wait until usart tx is idle
   hse-on! sys-clk-hse                   \ switch system clock over to hse
   flash-latency!                        \ set latency
   >R                                    \ save mhz for later
   prefetch-ena cache-init               \ setup prefetch and cache
   pll-off pll-src-hse                   \ turn off pll use hse clock for pll
   HSE_CLK #2000000 / pllm!              \ 2 MHz PLL input clock
   R@ PLLN! 0 PLLP! pll-on               \ PLL clock is double mhz due 2MHz PLLM
   0 hpre! 0 PPRE2! %100 PPRE1!          \ AHB-mhz APB2-mhz APB1-mhz/2
   sys-clk-pll                           \ turn sys clock back to pll
   USART_TERM #115200                    \ fix USART_TERM baud rate
   r> 1+ 2/ usart-set-baud ;             \ apb1 clock for USART_TERM is mhz/2 here

: clk-100-hse ( -- )                     \ set system clock to 100 MHz hse
   hse-on! sys-clk-hse
   3 flash-latency!                      \ 2V7 .. 3V6 100 MHz
   prefetch-ena cache-init
   pll-off pll-src-hse
   HSE_CLK #2000000 / pllm!              \ 2 MHz PLL input clock
   #100 PLLN! 0 PLLP!                    \ 200 MHz PLL clock 100 Mhz PLLP clock
   pll-on
   0 hpre! 0 PPRE2! %100 PPRE1!          \ AHB-100MHz APB2-100MHz APB1-50MHz
   sys-clk-pll
   USART_TERM #115200
   #50000000 usart-set-baud ;
: clk-96-hse ( -- )                      \ set system clock to 96 MHz hse
   hse-on! sys-clk-hse
   3 flash-latency!                      \ 2V7 .. 3V6 96 MHz
   prefetch-ena cache-init
   pll-off pll-src-hse
   HSE_CLK #2000000 / pllm!              \ 2 MHz PLL input clock
   #96 PLLN! 0 PLLP!                     \ 192 MHz PLL clock 96 Mhz PLLP clock
   pll-on
   0 hpre! 0 PPRE2! %100 PPRE1!          \ AHB-96MHz APB2-96MHz APB1-48MHz
   sys-clk-pll
   USART_TERM #115200
   #48000000 usart-set-baud ;
: delay  ( n -- ) 0 do loop ;            \ busy wait
